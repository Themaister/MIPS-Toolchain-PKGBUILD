# $Id$
# Creator Anatol Pomozov <anatol.pomozov@gmail.com>
# Adapt for MIPS64: Valent√≠n Kivachuk <vk18496@gmail.com>

_target=mips-linux-gnu
pkgname=$_target-binutils
pkgver=2.29
pkgrel=1
#_commit=2bd25930
pkgdesc='A set of programs to assemble and manipulate binary and object files for the MIPS64 target (Big Endian)'
arch=(i686 x86_64)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(zlib)
source=(ftp://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.bz2 force_disable_gnu_hash.patch)
sha1sums=('4376bce1e58d591c6a5dd44fc8713f154208d735'
          'e01ba32390e000b414f4758cfc0faba0faf9031a')

prepare() {
  cd binutils-$pkgver
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
  patch -p1 < "$srcdir"/force_disable_gnu_hash.patch
}

build() {
  cd binutils-$pkgver

  if [ "${CARCH}" != "i686" ];
  then
    # enabling gold linker at i686 makes the install fail
    enable_gold='--enable-gold'
  fi

  ./configure --target=$_target \
              --with-sysroot=/usr/$_target \
              --prefix=/usr \
              --disable-multilib \
              --with-gnu-as \
              --with-gnu-ld \
              --disable-nls \
              --enable-ld=default \
              $enable_gold \
              --enable-plugins \
              --enable-deterministic-archives

  make
}

check() {
  cd binutils-$pkgver
  
  # unset LDFLAGS as testsuite makes assumptions about which ones are active
  # do not abort on errors - manually check log files
  make -k LDFLAGS="" check || true
}

package() {
  cd binutils-$pkgver

  make DESTDIR="$pkgdir" install

  # Remove file conflicting with host binutils and manpages for MS Windows tools
  rm "$pkgdir"/usr/share/man/man1/$_target-{dlltool,nlmconv,windres,windmc}*

  # Remove info documents that conflict with host version
  rm -r "$pkgdir"/usr/share/info
}
